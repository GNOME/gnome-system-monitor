dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(gnome-system-monitor, 2.9.3)
AM_CONFIG_HEADER(config.h)

AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
GNOME_COMPILE_WARNINGS

LIBGNOME_REQUIRED=2.0.0
LIBGNOMEUI_REQUIRED=2.0.0
GCONF_REQUIRED=1.1.5
LIBWNCK_REQUIRED=2.5.0
LIBGTOP_REQUIRED=2.8.2
GTK_REQUIRED=2.5.0
GNOME_VFS_REQUIRED=2.6.0
LIBGNOMESU_REQUIRED=0.9.3

PKG_CHECK_MODULES(PROCMAN, libgnome-2.0 >= $LIBGNOME_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED gconf-2.0 >= $GCONF_REQUIRED libgtop-2.0 >= $LIBGTOP_REQUIRED libwnck-1.0 >= $LIBWNCK_REQUIRED gtk+-2.0 >= $GTK_REQUIRED gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED libgnomesu-1.0 >= $LIBGNOMESU_REQUIRED)


AC_ARG_ENABLE(more-warnings,
[  --enable-more-warnings  Maximum compiler warnings],
set_more_warnings="$enableval",[
    set_more_warnings=yes
])
AC_MSG_CHECKING(for more warnings, including -Werror)
if test "$GCC" = "yes" -a "$set_more_warnings" != "no"; then
    AC_MSG_RESULT(yes)
    CFLAGS="\
    -Wall \
    -Winline \
    -Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes \
    -Wnested-externs -Wpointer-arith \
    -Wcast-align -Wsign-compare \
    -std=gnu89 \
    $CFLAGS"

    for option in -Wno-strict-aliasing -Wno-sign-compare; do
        SAVE_CFLAGS="$CFLAGS"
        CFLAGS="$CFLAGS $option"
        AC_MSG_CHECKING([whether gcc understands $option])
        AC_TRY_COMPILE([], [],
            has_option=yes,
            has_option=no,)
        if test $has_option = no; then
            CFLAGS="$SAVE_CFLAGS"
        fi
        AC_MSG_RESULT($has_option)
        unset has_option
        unset SAVE_CFLAGS
    done
    unset option
else
    AC_MSG_RESULT(no)
fi


AC_SUBST(PROCMAN_CFLAGS)
AC_SUBST(PROCMAN_LIBS)

AM_GCONF_SOURCE_2

AC_DEFINE_UNQUOTED(GNOME_ICONDIR, "${prefix}/share/pixmaps")

dnl Add the languages which your application supports here.
ALL_LINGUAS="am ar az be bg bn bs ca cs cy da de el en_CA en_GB es eu fa fi fr ga gl gu he hi hr hu id it ja ko li lt lv mk ml mn ms nb ne nl no pa pl pt pt_BR ro ru sk sl sq sr sr@Latn sv ta th tr uk vi zh_CN zh_TW"

GETTEXT_PACKAGE=gnome-system-monitor
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")
AM_GLIB_GNU_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

AC_ARG_ENABLE(selinux, AC_HELP_STRING(--enable-selinux,enable SELinux support),enable_selinux=$enableval, enable_selinux=no)

if test x$enable_selinux = xyes; then
   AC_DEFINE(ENABLE_SELINUX, 1, [Define to 1 to enable SELinux support])
   AM_CONDITIONAL(ENABLE_SELINUX, test x$enable_selinux = xyes)
   AC_MSG_RESULT(SELinux support enabled)
else
   AC_MSG_RESULT(SELinux support disabled)
fi


dnl Honor ACLOCAL_FLAGS for --enable-maintainer-mode
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
help/Makefile
help/C/Makefile
gnome-system-monitor.desktop.in
])

